:- use_module(library(clpfd)).

solve_puzzle(Zeb, Wat) :-
    Color = [Red, Yel, Blu, Gre, Whi],
    Country = [Eng, Spa, Nor, Ukr, Por],
    Drink = [Ora, Tea, Cof, Mil, Wat],
    Cigarette = [Mar, Che, Win, Luk, Sgl],
    Animal = [Dog, Fox, Igu, Hor, Zeb],
    append(Color, Country, L1),
    append(Drink, Cigarette, L2),
    append(L1, L2, L3),
    append(L3, Animal, All),
    domain(All, 1, 5),
    all_distinct(Color),
    all_distinct(Country),
    all_distinct(Drink),
    all_distinct(Cigarette),
    all_distinct(Animal),
    Eng #= Red,
    Spa #= Dog,
    Nor #= 1,
    Yel #= Mar,
    abs(Che - Fox) #= 1,
    Nor #= Blu - 1,
    Win #= Igu,
    Luk #= Ora,
    Ukr #= Tea,
    Por #= Sgl,
    abs(Mar - Hor) #= 1,
    Gre #= Cof,
    Gre #= Whi + 1,
    Mil #= 3,
    labeling([], All).